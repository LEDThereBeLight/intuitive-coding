---
layout: post
title: "The View"
subtitle: "What our model looks like"
section: app-building
comments: true
---

### Using the model in the *view*

A model doesn't do any good on its own. We need to feed it into the program.

How do we do that? As we have our program set up right now, all of our page components are sitting inside `main`. It may not have been obvious until now, but our entire `main` definition has a single purpose - it shows a webpage. So instead of leaving everything in `main` cluttering it up, we're going to define a new function and replace our code in `main` with that.

So what should we call this new function? We don't have to be creative - programmers have a name for it, the **view**. The view is everything that the user can see at the moment.

![everest](https://upload.wikimedia.org/wikipedia/commons/8/8c/K2,_Mount_Godwin_Austen,_Chogori,_Savage_Mountain.jpg)
*a view of the Everest model*

We need to make a view function. Whenever we create a function, we ask two questions:

  1. What should the function give back when it's done running?
  2. What information does the function need to do its job?

So what should `view` give back when it's done running? Well, we want to use our view to create a webpage, so the output of the function will be a value of type Html that `main` can use.

And what information does `view` need to do its job? It needs to know about the model, the current state of the program, so that it can decide what to show on the screen.

In other words,

`view` is a function that takes a `model` as input and produces an expression of type Html.
{: .info}

In case you're skeptical about having to do this extra work to split out the view from `main`, there really is a good reason for it. In our app it doesn't much matter, but in real applications we separate out the view, or the **front end**, so that it can be easily replaced without affecting the functionality, or the **back end**, of the app. It's typical to go through several iterations of designs, and it's much easier to deploy redesigns when the look and feel isn't baked into how the app works - you know exactly where to go to make changes.

### Defining a function with inputs

So far, we've *used* functions that accept inputs, but we haven't *defined* any ourselves. Fortunately, it's pretty easy to do in Elm - much easier than in most other languages. We just put the names of the inputs after the name of the variable.

Here's an example of defining a function that takes two numbers and adds them:

{% highlight haskell %}
add one two = one + two
{% endhighlight %}

`one` and `two` are inputs to the `add` function, so they go on the left side of the `=` sign. They're then both used in the function definition expression on the right side.

Once we've defined our function, we can use it by calling it and giving it two inputs, just like we would with any other function.

{% highlight haskell %}
add 1 2 -- call add with 1 and 2, giving us back 3

add 15 15 -- call add with 15 and 15, giving us back 30
{% endhighlight %}

There's something we need to keep in mind when defining functions that take inputs. It takes awhile to get comfortable with, but is extremely important:

Input names are ONLY accessible in the function definition. We can't call those specific variable names anywhere else in our program!
{: .info}

For example, writing something like this won't work:

{% highlight haskell %}
import Html

add one two = one + two

main = Html.text one
{% endhighlight %}

You'll get this error:

![one undefined](images/number-undefined.png)

This idea in programming, where specific words are only defined and "useable" in certain areas of the program, is called **scope**. As in, "the **scope** of `one` and `two` is limited to the definition of the function `add`."

Okay, I think it's finally time to make the `view`.

### Defining our view

Let's make a new variable called `view` that takes one input, the model, and define it to be everything we currently have in `main`.

{% highlight haskell %}
view model = Html.div
  [ pageStyling ]
  [
    Html.h1 [ headingStyling ] [ Html.text "cats" ],
    Html.input [ searchboxStyling ] [],
    Html.button [ buttonStyling ] [ Html.text "cats" ],
    Html.button [ buttonStyling ] [ Html.text "dogs" ],
    Html.button [ buttonStyling ] [ Html.text "ice cream" ],
    Html.br [] [],
    Html.br [] [],
    Html.img [ imageStyling, Html.Attributes.src "https://media.giphy.com/media/11s7Ke7jcNxCHS/giphy.gif" ] []
  ]
{% endhighlight %}

Now we can replace the definition of `main` with the following:
{% highlight haskell %}
main = view model
{% endhighlight %}

We're defining view to accept an input which we'll call "model" for use in our definition. Note that even though the input we give `view` in `main` has the same name as the `model` we defined up above, these are different variables, which goes back to the idea of *scope*.

### Using the model in the view

We need to replace the static, unchanging parts of our app with content from the model.

Remember, our model has two parts:
  1. title text
  2. an image location

And it looks like this:
{% highlight haskell %}
model = ("cats", "https://media.giphy.com/media/ND6xkVPaj8tHO/giphy.gif")
{% endhighlight %}

So how do we get the two parts out of the model?

Our model is a Tuple, and the Tuple module comes with two handy functions: `first` and `second`. The Tuple module is always available in Elm, so we don't need to import it.

So to get the title, we can say

{% highlight haskell %}
Tuple.first model
{% endhighlight %}

And to get the image location, we can say

{% highlight haskell %}
Tuple.second model
{% endhighlight %}

### Replacing the title text with the model

Change the heading from static text to the first part of the Tuple.

{% highlight haskell %}
Html.h1 [ headingStyling ] [ Tuple.first model ]
{% endhighlight %}

Refresh the page. What happens?

![type mismatch](images/tuple-first-error.png)

Elm is pretty good with error messages, but this one is a little cryptic if you don't know what you're looking for. Remember that the second argument to `Html` functions is a list of children `Html` elements. So `Html.h1` is looking for a list of `Html` values, but we're giving it a list of Strings (since `Tuple.first model` is a String type).

We need to convert this String into an Html value. We already know how to do this - we can use the `Html.text` function.

{% highlight haskell %}
Html.h1 [ headingStyling ] [ Html.text Tuple.first model ]
{% endhighlight %}

Now run the program. What happens?

![missing-parens](images/missing-parens-error.png)

Oops. We get two errors.

The first error is a little confusing, but it actually comes as a result of the second, so we can ignore it for now. Knowing which errors are root causes and which are just symptoms comes with experience.

The second error says,

"Function `text` is expecting 1 argument, but was given 2."
{: .important}

Let's talk about what this means.

**Argument** is just a synonym for "input". It's what we are giving to the function when we call it.

So Elm thinks we're giving `Html.text` two inputs, when we really mean to give it one. The issue is that we have a space between them, so Elm thinks `Tuple.first` and `model` are separate. So we need to group these two terms into one expression. Fortunately, Elm gives us a hint on how to do that. Below the error, it says,

"Maybe you forgot some parentheses? Or a comma?"
{: .important}

We can put both terms inside parentheses to group them. Go ahead and wrap the `Tuple.first model` expression in parentheses.

{% highlight haskell %}
Html.h1 [ headingStyling ] [ Html.text (Tuple.first model) ]
{% endhighlight %}

Note that these parentheses don't make a Tuple - there is no comma separating the terms. These parentheses just tell Elm to group the words together into a single expression.

### Replacing the image location with the model

Finally, let's change the image's source from static text to the second part of the Tuple.

{% highlight haskell %}
Html.img [ imageStyling, Html.Attributes.src (Tuple.second model) ] []
{% endhighlight %}

Whew, that was a lot! I'm exhausted just from writing that, so don't be frustrated if it doesn't make sense your first time through. It takes *everyone* several repetitions for it to sink in. You'll be amazed how something seemingly impossible can seem easy the second or third time around. Now go eat a cookie, you deserve it!

![cookie](https://media.giphy.com/media/hpfInSxkkLdpS/giphy.gif)

### Code so far

Here's our code so far:

{% highlight haskell %}
module Main exposing (..)
import Html
import Html.Attributes

main = view model

model = ("cats", "https://media.giphy.com/media/ND6xkVPaj8tHO/giphy.gif")

view model = Html.div
  [ pageStyling ]
  [
    Html.h1 [ headingStyling ] [ Html.text (Tuple.first model) ],
    Html.button [ buttonStyling ] [ Html.text "cats" ],
    Html.button [ buttonStyling ] [ Html.text "dogs" ],
    Html.button [ buttonStyling ] [ Html.text "ice cream" ],    
    Html.br [] [],
    Html.br [] [],
    Html.img [ imageStyling, Html.Attributes.src (Tuple.second model) ] []
  ]

-- Styling

pageStyling = Html.Attributes.style [ ("text-align", "center") ]

headingStyling =
  Html.Attributes.style
    [
      ("font-size", "2em"),
      ("color", "#333"),
      ("padding-bottom", "30px")  
    ]

buttonStyling =
  Html.Attributes.style
    [
      ("padding", "10px"),
      ("background-color", "#99ddff"),
      ("border-radius", "2px"),
      ("border", "1px solid #99ddff"),
      ("color", "white"),
      ("font-size", "1.5em"),
      ("margin", "5px"),
      ("cursor", "pointer")
    ]

imageStyling =
  Html.Attributes.style
    [
      ("border-radius", "2px"),
      ("box-shadow", "0 2px 4px rgba(0,0,0,0.12), 0 2px 3px rgba(0,0,0,0.24)")
    ]
{% endhighlight %}

Go ahead and commit your changes.

{% highlight shell %}
$ git add -A
$ git commit -m "Add model and view"
{% endhighlight %}
